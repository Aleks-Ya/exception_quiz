//Run: gradle
//Run: sudo gradle installLinux

import java.nio.file.Files
import java.nio.file.Path
import java.nio.file.Paths

apply plugin: 'java'
sourceSets.main.java.srcDir 'src'
sourceSets.test.java.srcDir 'test'
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7
defaultTasks('build')

group = "exceptionquize"
version = "2.0"
jar.manifest.attributes "Main-Class": "exceptionquiz.ExceptionQuiz"

final def installDir = '/opt/exception_quiz'
final def shName = 'run.sh'
final def shInstall = new File(installDir, shName)

task installLinux(dependsOn: build, type: Copy) {
    from jar.archivePath.parent
    into installDir
    filesMatching('*.sh') {
        fileMode = 0755
    }

    doFirst() {
        File shFile = new File(jar.archivePath.parent, shName)
        shFile.withWriter { out ->
            out.writeLine('#!/bin/bash')
            out.writeLine("java -jar $installDir/$jar.archiveName")
        }
    }

    doLast() {
        Path link = Paths.get('/usr/bin/exception_quiz');
        Path target = shInstall.toPath()
        Files.deleteIfExists(link)
        Files.createSymbolicLink(link, target)
    }
}

dependencies {
    testCompile 'junit:junit:4.11'
}
repositories {
    mavenCentral()
}